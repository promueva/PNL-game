***(

File: game.maude

Definition of a Maude's strategy to efficiently explore the search tree

These strategies are more efficient using dsrew. Note that the rule SELECT
allows for controlling the non-determinism on selecting the next sequent to be
selected. It is sufficient to consider "only" one solution since, when it
fails, the next one will be chosen. 

***)

load ./semantics .

smod GAME-STRAT is
    protecting GAME-SEMANTICS .

    strat expand-tree : @ Tree .
    strat make-me-win : @ Tree .
    strat det-str : @ Tree .

    --- This is completely deterministic since all the 
    --- branches are explored 
    sd expand-tree :=
    ( one ( ATOM | TREE-ALL | NEG | Y | I)) ! .


    --- This is also deterministic 
    sd det-str := 
    ( one (
            ATOM |   --- Mark all the leaves if any  
            NEG  |   --- This is deterministic 
            TREE |   --- Determine winning states if any 
            Y    |   --- All your choices need to be explored 
            I        --- Expand my choices (possibly without selecting one)
     ) ) ! .

    --- Stop when a solution has been found 
    sd make-me-win :=
    det-str ; 
    ( match YT:YTree ? fail : --- You win the root and fail
      match IT:ITree ? idle : --- I win the root and stop 
      (one(SELECT) ; make-me-win)       --- otherwise, make an I-move and continue
    ) .

endsm
